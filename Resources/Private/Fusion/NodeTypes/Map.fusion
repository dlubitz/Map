prototype(Ttree.Map:Map) < prototype(Neos.Neos:Content) {
	templatePath = 'resource://Ttree.Map/Private/Templates/NodeTypes/Map.html'

	identifier = ${String.md5(q(node).property('_path'))}
	defaultAddress = NULL
	address = ${q(node).property('defaultAddress') ? q(node).property('defaultAddress') : this.defaultAddress}
	@context.address = ${this.address}

	width = '100%'
	height = '300px'

	@context {
		address = ${this.address}
		identifier = ${this.identifier}

		width = ${q(node).property('width') ? q(node).property('width') : this.width}
		height = ${q(node).property('height') ? q(node).property('height') : this.height}
	}

	attributes = Neos.Fusion:Attributes {
		id = ${"ttree-map-" + identifier}
		data-identifier = ${identifier}

		class {
			ttree-map = 'ttree-map'
		}
		class.@process.appendClassUsedByJavascript = ${Array.concat(value ,' ttree-map-configuration-js')}

		style = ${"height: " + height + "; width: " + width + ";"}

		data-ttree-map-configuration = Ttree.Map:Configuration {
			address = ${address}
			map = ${node}

			@process.rawUrlEncode = ${String.rawUrlEncode(value)}
		}
	}
}
